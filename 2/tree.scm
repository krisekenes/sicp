(define (make-tree entry left right)
  (list entry left right))
(define (entry tree)
  (car tree))
(define (left-branch tree)
  (cadr tree))
(define (right-branch tree)
  (caddr tree))

(define (element-of-set? val tree)
  (cond ((null? tree) #f)
        ((= val (entry tree)) #t)
        ((< val (entry tree)) (element-of-set? val (left-branch tree)))
        (else (element-of-set? val (right-branch tree)))))

(define (adjoin-set val tree)
  (cond ((null? tree) (make-tree val `() `()))
        ((= val (entry tree)) tree)
        ((< val (entry tree)) (make-tree (entry tree) (adjoin-set val (left-branch tree)) (right-branch tree)))
        (else (make-tree (entry tree) (left-branch tree) (adjoin-set val (right-branch tree))))))


(define (tree->list-1 tree)
  (if (null? tree) `()
      (append (tree->list-1 (left-branch tree))
              (cons (entry tree) (tree->list-1 (right-branch tree))))))

(define (tree->list-2 tree)
  (define (copy-to-list tree result)
    (if (null? tree) result
        (copy-to-list (left-branch tree) (cons (entry tree) (copy-to-list (right-branch tree) result)))))
  (copy-to-list tree `()))


(define (list->tree elements)
  (car (partial-tree elements (length elements))))

(define (partial-tree elts n)
  (if (= n 0)
      (cons `() elts)
      (let ((left-size (quotient (- n 1) 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts)
                                              right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree)
                      remaining-elts))))))))
